---
title: Experiment 07
tags: net dsl ddd
---

The second post in the **domain specific language** series demonstrates how you can use F# computation expressions to create an embedded language.

## Introduction

*Please read the [first post]({% link _posts/2022-10-01-DSL1.md %}) in this series before continuing.*

This post demonstrates how to create a custom [computation expression](https://learn.microsoft.com/en-us/dotnet/fsharp/language-reference/computation-expressions) to capture data. You may have already used computation expressions without knowing it -  `seq {}` and `async {}` are both built using computation expressions. Computation expressions  provide users with a way to extend F#. This ability is most similar to macros or metaprogramming abilities of other languages (e.g. LISP, Rust, Ruby, Template Haskell), but I find computation expressions more straightforward to understand and use. In this post, we will see how to create three kinds of computation expressions to model our data. 

Before we learn how to create a custom computation expression, it is best to understand *why* we would want to do so. Our first example uses computation expressions to create a concise record syntax. 

{% highlight fsharp %}
let trades = 
  trade{
    Buy 4 MSFT 258.32 AllOrNone
    Sell 3 META 158.71 AllOrNone
    Sell 6 GOOGL 106.08 AllOrNone
  }

{% endhighlight %}

## Data model #2a (Concise record syntax)

The expression above creates the same list of `Trade` records as in the first post without having to specify the record's field names. We might choose this data model if we want a *concise* record syntax. 

We accomplish this syntax by creating a [custom computation expression](https://learn.microsoft.com/en-us/dotnet/fsharp/language-reference/computation-expressions#extending-existing-builders-with-new-custom-operations). First, define a `TradeBuilder` class and provide an implementation for `Yield`[^1]. Then implement two custom operations (tagged with the [`CustomOperation`](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-core-customoperationattribute.html) attribute). Due to the domain and record layout, I decided it was natural to use `Buy` and `Sell` as keywords to create the record. Each method takes 5 parameters: the first argument captures trades that were created above the current trade but within the same computation expression; it has type `seq<Trade>`. The next 4 parameters match those of the record (except `price`, which takes a `float` and is converted to `decimal`). 

{% highlight fsharp %}
module Trade = 

  type TradeBuilder() =

      member _.Yield _ = []

      [<CustomOperation("Buy")>]
      member _.Buy (previous: seq<Trade>, numShares:int, ticker:Stocks, price:float, allOrNone:Portion) = 
        [yield! previous
         yield {buyOrSell=Buy; ticker=ticker;numShares=numShares;price=decimal(price);allOrNone=(allOrNone=AllOrNone)}]

      [<CustomOperation("Sell")>]
      member _.Sell (previous: seq<Trade>, numShares:int, ticker:Stocks, price:float, allOrNone:Portion) = 
        [yield! previous
         yield {buyOrSell=Sell; ticker=ticker;numShares=numShares;price=decimal(price);allOrNone=(allOrNone=AllOrNone)}]
{% endhighlight %}

Before we can use the computation expression, we must initialize it with:

{% highlight fsharp %}
let trade = Trade.TradeBuilder()

let trades = 
  trade{
    Buy 4 MSFT 258.32 AllOrNone
    Sell 3 META 158.71 AllOrNone
    Sell 6 GOOGL 106.08 AllOrNone
  }

{% endhighlight %}

Then if we run `Trade.tradeMany trades` we get the correct output:

{% highlight console %}
You just purchased 4 shares of MSFT for a total cost of $1,033.28.
You just sold 3 shares of META for a total earnings of $476.13.
You just sold 6 shares of GOOGL for a total earnings of $636.48.
-------------------------------------------------------------------
You just executed a series of trades that earned you $79.33.
{% endhighlight %}

## Data model #2b (Concise record syntax with placeholders)

Our first attempt at a data model has a serious flaw - without specifying the field names it could be very easy to forget to include a field or put it in the wrong order. Imagine a record with 5 fields all the same type (e.g. decimal), how would you keep them straight? In some domains it may be appropriate to add a few placeholder keywords to help keep things straight. In this example, the following probably reads much better to domain experts:

{% highlight fsharp %}
let trades = 
  trade{
    Buy 4 SharesOf MSFT At 258.32 AllOrNone
    Sell 3 SharesOf META At 158.71 AllOrNone
    Sell 6 SharesOf GOOGL At 106.08 AllOrNone
  }
{% endhighlight %}

The `SharesOf` keyword tells you 2 things - the previous parameter was the number of shares and the following parameter is company. The `At` keyword separates the price constraints for the trade. This is still much more concise than specifying all the field names and it improves readability. To update  the `TradeBuilder` code we need to define two new types (`SharesOf` and `At`) and add them to the parameter list of each method.

{% highlight fsharp %}
type SharesOf = SharesOf
type At = At

module Trade = 

  type TradeBuilder() =

      member _.Yield _ = []

      [<CustomOperation("Buy")>]
      member _.Buy (previous: seq<Trade>, numShares:int, sharesOf:SharesOf, ticker:Stocks, at:At, price:float, allOrNone:Portion) = 
        [yield! previous
         yield {buyOrSell=Buy; ticker=ticker;numShares=numShares;price=decimal(price);allOrNone=(allOrNone=AllOrNone)}]

      [<CustomOperation("Sell")>]
      member _.Sell (previous: seq<Trade>, numShares:int, sharesOf:SharesOf, ticker:Stocks, at:At, price:float, allOrNone:Portion) = 
        [yield! previous
         yield {buyOrSell=Sell; ticker=ticker;numShares=numShares;price=decimal(price);allOrNone=(allOrNone=AllOrNone)}]
        
{% endhighlight %}

## Data model #2c (Fluent record syntax)

The final computation expression uses the [Builder Design Pattern](https://refactoring.guru/design-patterns/builder) and a Fluent expression style to create trades. This approach is most useful when you can set default values for everything and then specify a subset of fields to update (similar to the `{default with ...}` record syntax). It also provides a way to set fields in any order. But unlike the previous two computation expressions, this approach only creates a single trade per expression. 

{% highlight fsharp %}
let trades = 
  [
    trade {
      Buy 4
      SharesOf MSFT
      At 258.32
      AllOrNone
    };
    //AllOrNone optional
    trade {
      Sell 3
      SharesOf META
      At 158.71      
    };
    // order-independent
    trade {
      AllOrNone
      At 106.08      
      SharesOf GOOGL
      Sell 6
    };
  ]
        
{% endhighlight %}

As you might have guessed, the `TradeBuilder` code is significantly different. `Yield` now provides the default record and we must specify custom operations for `Buy`, `Sell`, `SharesOf`, `At`, `AllOrNone`, and `Partial`. Each method takes the current trade and updates the corresponding field.  

{% highlight fsharp %}
module Trade = 

  let baseTrade = {buyOrSell = Buy; ticker=MSFT; numShares=0; price=0.0m; allOrNone=true}

  type TradeBuilder() =

      member _.Yield _ = baseTrade

      [<CustomOperation("Buy")>]
      member _.Buy (trade: Trade, input: int) = {trade with numShares = input; buyOrSell = Buy}

      [<CustomOperation("Sell")>]
      member _.Sell (trade: Trade, input: int) = {trade with numShares = input; buyOrSell = Sell}

      [<CustomOperation("SharesOf")>]
      member _.Ticker (trade: Trade, input: Stocks) = {trade with ticker = input}

      [<CustomOperation("At")>]
      member _.Price (trade: Trade, input: float) = {trade with price = decimal(input)}

      [<CustomOperation("AllOrNone")>]
      member _.AllOrNone (trade: Trade) = {trade with allOrNone = true}
    
      [<CustomOperation("Partial")>]
      member _.Partial (trade: Trade) = {trade with allOrNone = false}
        
{% endhighlight %}

The code listing for this series can be found [here](https://github.com/dlfelps/dsl-examples).

## Some other examples/references

In my opinion, custom computational expressions are an underutilized feature of F#. They are much more flexible/powerful than I are showing here so if you would like learn more please check out the following resources that use custom computation expressions.

[Computation expressions workshop](https://github.com/panesofglass/computation-expressions-workshop)

[Computation Expressions Explained - Youtube](https://www.youtube.com/watch?v=pC4ZIeOmgB0&t=2461s)

[Lego Mindstorms DSL](https://atlemann.github.io/fsharp/2019/12/11/mindstorms-dsl.html)

[FsHttp CE](https://github.com/fsprojects/FsHttp/blob/master/src/FsHttp/Dsl.CE.fs)


## Next

In the final post of this series we will learn about the difference between an internal and external domain specific language and use [FParsec](https://github.com/stephan-tolksdorf/fparsec) to load records after compile time! 

#### Footnotes

[^1]: https://learn.microsoft.com/en-us/dotnet/fsharp/language-reference/computation-expressions#creating-a-new-type-of-computation-expression